buildscript {
    ext {
        springBootVersion = '2.0.1.RELEASE'
        querydslVersion = '4.1.4'
    }
    repositories {
        mavenCentral()
        maven {url "https://plugins.gradle.org/m2/"}
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("gradle.plugin.com.ewerk.gradle.plugins:querydsl-plugin:1.0.9")
	    classpath files("../engine/build/libs/engine-1.0.0.jar")
    }
}

plugins {
	id 'io.franzbecker.gradle-lombok' version '1.12'
}

subprojects {
	
	apply plugin: 'io.franzbecker.gradle-lombok'
    apply plugin: 'com.ewerk.gradle.plugins.querydsl'
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
	apply plugin: com.giteshdalal.serve.EnginePlugin
	
    sourceCompatibility = 1.8
	
	querydsl {
	    jpa = true
	}
	
	afterEvaluate {
		println "Evaluated $project"
	    project.tasks.compileQuerydsl.options.compilerArgs = [
	            "-proc:only",
	            "-processor",
	            project.querydsl.processors() + ',lombok.launch.AnnotationProcessorHider$AnnotationProcessor'
	    ]
	}
	
	project.tasks.compileQuerydsl.dependsOn(project.tasks.serve)

    repositories {
        mavenCentral()
    }

    dependencies {
        compile('org.projectlombok:lombok:1.16.20')
        compile('org.modelmapper:modelmapper:1.1.0')
        compile('org.springframework.boot:spring-boot-starter')
	    compile("com.querydsl:querydsl-root:$querydslVersion")
	    compile("com.querydsl:querydsl-jpa:$querydslVersion")
	    compile("com.querydsl:querydsl-apt:$querydslVersion:jpa")
        testCompile('org.springframework.boot:spring-boot-starter-test')
    }
	
    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:Finchley.M9"
        }
    }

    repositories {
        maven {
            url 'https://repo.spring.io/libs-milestone'
        }
    }

    eclipse {
        classpath {
            containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
            containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
        }
    }
}