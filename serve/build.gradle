buildscript {
    ext {
        lombockGradleVersion = '1.12'
        lombockVersion = '1.16.20'
        modelMapperVersion = '2.3.0'
        springBootVersion = '2.0.1.RELEASE'
        cloudVersion = 'Finchley.M9'
        queryPluginVersion = '1.0.10'
        querydslVersion = '4.1.4'
        serveVersion = '1.0.0'
    }
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath("io.franzbecker:gradle-lombok:${lombockGradleVersion}")
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("gradle.plugin.com.ewerk.gradle.plugins:querydsl-plugin:${queryPluginVersion}")
        classpath files("../engine/build/libs/engine-${serveVersion}.jar")
    }
}

apply plugin: com.giteshdalal.serve.ServiceGeneratorPlugin

subprojects {
    apply plugin: 'io.franzbecker.gradle-lombok'
    apply plugin: 'com.ewerk.gradle.plugins.querydsl'
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: com.giteshdalal.serve.EnginePlugin

    sourceCompatibility = 1.8

    sourceSets {
        main {
            java {
                srcDir "$buildDir/generated-sources/apt"
            }
        }
    }
    
    querydsl {
        jpa = true
        querydslSourcesDir = "$buildDir/generated-sources/apt"
    }

    afterEvaluate {
        println "Evaluated $project"
        project.tasks.compileQuerydsl.options.compilerArgs = [
                "-proc:only",
                "-processor",
                project.querydsl.processors() + ',lombok.launch.AnnotationProcessorHider$AnnotationProcessor'
        ]
    }

    project.tasks.compileQuerydsl.dependsOn(project.tasks.serve)

    repositories {
        mavenCentral()
    }

    dependencies {
        compile("org.projectlombok:lombok:$lombockVersion")
        compile("org.modelmapper:modelmapper:$modelMapperVersion")
        compile("org.modelmapper.extensions:modelmapper-jackson:$modelMapperVersion")
        compile('org.springframework.boot:spring-boot-starter')
        compile("com.querydsl:querydsl-root:$querydslVersion")
        compile("com.querydsl:querydsl-jpa:$querydslVersion")
        compile("com.querydsl:querydsl-apt:$querydslVersion:jpa")
        compile("com.fasterxml.jackson.module:jackson-module-jsonSchema")
        testCompile('org.springframework.boot:spring-boot-starter-test')
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:$cloudVersion"
        }
    }

    repositories {
        maven {
            url 'https://repo.spring.io/libs-milestone'
        }
    }

    test {
        testLogging {
            showStandardStreams = true
        }
    }

    eclipse {
        classpath {
            containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
            containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
        }
    }
}